MODULE ANISOHYPERELASTIC

  IMPLICIT NONE
  INTEGER, PARAMETER :: DP=SELECTED_REAL_KIND(14)
  REAL(KIND=DP), PARAMETER :: HALF=.5_DP, MONE=-1._DP
  REAL(KIND=DP), PARAMETER :: ZERO=0._DP, ONE=1._DP, TWO=2._DP, THREE=3._DP
  REAL(KIND=DP), PARAMETER :: FOUR=4._DP
  REAL(KIND=DP), PARAMETER :: THIRD=ONE/THREE, P2THIRD=TWO/THREE, P3HALF=THREE/TWO
  REAL(KIND=DP), PARAMETER :: FOURTH=ONE/FOUR, SIX=6._DP
  REAL(KIND=DP), PARAMETER :: IIMI4(6,6) = RESHAPE(&
                         [ZERO,  ONE,  ONE, ZERO, ZERO, ZERO, &
                           ONE, ZERO,  ONE, ZERO, ZERO, ZERO, &
                           ONE,  ONE, ZERO, ZERO, ZERO, ZERO, &
                          ZERO, ZERO, ZERO, MONE, ZERO, ZERO, &
                          ZERO, ZERO, ZERO, ZERO, MONE, ZERO, &
                          ZERO, ZERO, ZERO, ZERO, ZERO, MONE], SHAPE(IIMI4))

CONTAINS

  SUBROUTINE ANISOHYPEREL(NPROPS, PROPS, TEMP, F, NOEL, CMNAME, INCMPFLAG, &
       IHYBFLAG, NFIBERS, FIBER_DIR, NSTATV, STATEV, NFLDV, FIELDV, DFIELDV, STRESS)
    ! ----------------------------------------------------------------------- !
    ! ANISOTROPIC HYPERELASTIC MATERIAL MODEL
    ! CALLS A UYHPER MODEL AND FORMULATES THE STRESS AND STIFFNESS ARRAYS
    ! ----------------------------------------------------------------------- !
    USE TENSALG, ONLY : DET,INVARS,PUSH,INV,DYAD,SYMSQ,I6,D_LA_RV,ASARRAY
    ! --- PASSED ARGUMENTS
    CHARACTER*8, INTENT(IN) :: CMNAME
    INTEGER, INTENT(IN) :: NPROPS, NOEL, NSTATV, NFLDV, NFIBERS
    INTEGER, INTENT(IN) :: INCMPFLAG, IHYBFLAG
    REAL(KIND=DP), INTENT(IN) :: PROPS(NPROPS), TEMP, F(3,3), FIBER_DIR(NFIBERS,3)
    REAL(KIND=DP), INTENT(INOUT) :: STATEV(NSTATV), FIELDV(NFLDV), DFIELDV(NFLDV)
    REAL(KIND=DP), INTENT(OUT) :: STRESS(6)
    ! --- LOCAL VARIABLES
    INTEGER :: I
    INTEGER, PARAMETER :: NINV=5, NINVA=NINV*(NINV+1)/2
    REAL(KIND=DP) :: JAC, C(6), PK2(6), DDTDDC(6,6)
    REAL(KIND=DP) :: U(2), DU(NINV), D2U(NINVA), D3U(NINVA)
    REAL(KIND=DP) :: I1, I2, I3, I4, I5, I1B, I2B, I4B, I5B, AINV(NINV)
    REAL(KIND=DP) :: CN(3), NN1(6), NN2(6), NN3(6)
    REAL(KIND=DP) :: SCALE, CI(6), B(NINV,6)
    REAL(KIND=DP), PARAMETER :: ZETA(1)=(/ZERO/)
    ! ------------------------------------------------------ ANISOHYPEREL --- !

    ! DEFORMATION TENSOR
    ! C = FT.F
    C = SYMSQ(F)

    ! JACOBIAN
    JAC = DET(F)

    ! INVARIANTS OF C
    CALL INVARS(C, FIBER_DIR(1,:), I1, I2, I3, I4, I5)

    ! INVARIANTS OF CBAR
    SCALE = JAC ** (-ONE / THREE)
    I1B = I1 * (SCALE ** 2)
    I2B = I2 * (SCALE ** 4)
    I4B = I4 * (SCALE ** 2)
    I5B = I5 * (SCALE ** 4)
    AINV = (/I1B, I2B, JAC, I4B, I5B/)

    CALL UANISOHYPER_INV(AINV, U, ZETA, NFIBERS, NINV, DU, D2U, D3U,&
         TEMP, NOEL, CMNAME, INCMPFLAG, IHYBFLAG, NSTATV, STATEV, &
         NFLDV, FIELDV, DFIELDV, NPROPS, PROPS)

    ! UPDATE THE STRESS AND MATERIAL STIFFNESS
    ! THE NOTATION BELOWS FOLLOWS THE APPENDIX OF ***TITLE*** WHERE
    ! THE SECOND PIOLA-KIRCHOFF STRESS IS GIVEN BY
    !
    !                           DU   DU  DIB
    !                   PK2 = 2 -- = --- --- = A.B
    !                           DC   DIB DC
    !
    ! WHERE A IS AN ARRAY CONTAINING THE DERIVATIVES OF U WRT ISOCHORIC
    ! INVARIANTS AND B IS AN ARRAY OF TENSORS CONTAINING THE DERIVATIVES OF
    ! THE ISOCHORIC INVARIANTS WRT C

    ! HELPER QUANTITIES
    CI = INV(C)

    CN = D_LA_RV(C, FIBER_DIR(1,:))
    NN1 = ASARRAY(DYAD(FIBER_DIR(1,:), FIBER_DIR(1,:)))
    NN2 = ASARRAY(DYAD(FIBER_DIR(1,:), CN))
    NN3 = ASARRAY(DYAD(CN, FIBER_DIR(1,:)))

    B(1,1:6) = SCALE ** 2 * (I6 - THIRD * I1 * CI)
    B(2,1:6) = SCALE ** 4 * (I1 * I6 - C - P2THIRD * I2 * CI)
    B(3,1:6) = HALF * JAC * CI
    B(4,1:6) = SCALE ** 2 * (NN1 - THIRD * I4 * CI)
    B(5,1:6) = SCALE ** 4 * (NN2 + NN3 - P2THIRD * I5 * CI)

    ! SECOND PIOLA-KIRCHHOFF STRESS: DERIVATIVE OF ENERGY WRT C
    FORALL(I=1:6) PK2(I) = TWO * SUM(DU(1:5) * B(1:5, I))

    ! CAUCHY STRESS
    STRESS = PUSH(F, PK2)

  END SUBROUTINE ANISOHYPEREL

  ! ************************************************************************* !

  SUBROUTINE JACOBIAN(NPROPS, PROPS, TEMP, F, NOEL, CMNAME, INCMPFLAG, &
       IHYBFLAG, NFIBERS, FIBER_DIR, NSTATV, STATEV, NFLDV, FIELDV, DFIELDV, DDSDDE)
    ! THIS PROCEDURE NUMERICALLY COMPUTES AND RETURNS THE JACOBIAN MATRIX
    !
    !                      J = (JIJ) = (DSIGI/DEPSJ).
    !
    ! THE COMPONENTS OF JSUB ARE COMPUTED NUMERICALLY USING A CENTERED
    ! DIFFERENCING SCHEME WHICH REQUIRES TWO CALLS TO THE MATERIAL MODEL
    ! SUBROUTINE FOR EACH ELEMENT OF PK2. THE CENTERING IS ABOUT THE POINT
    !                       EPS = EPSOLD + D * DT,
    ! WHERE D IS THE RATE-OF-STRAIN ARRAY.
    REAL(KIND=DP), PARAMETER :: TOL=1.E-6_DP
    CHARACTER*8, INTENT(IN) :: CMNAME
    INTEGER, INTENT(IN) :: NPROPS, NOEL, NSTATV, NFLDV, NFIBERS
    INTEGER, INTENT(IN) :: INCMPFLAG, IHYBFLAG
    REAL(KIND=DP), INTENT(IN) :: PROPS(NPROPS), TEMP, STATEV(NSTATV)
    REAL(KIND=DP), INTENT(IN) :: FIBER_DIR(NFIBERS,3)
    REAL(KIND=DP), INTENT(INOUT) :: FIELDV(NFLDV), DFIELDV(NFLDV)
    REAL(KIND=DP), INTENT(IN) :: F(3,3)
    REAL(KIND=DP), INTENT(OUT) :: DDSDDE(6,6)
    INTEGER :: I
    REAL(KIND=DP) :: EPS, DF(3,3)
    REAL(KIND=DP) :: SVP(NSTATV), FP(3,3), SP(6)
    REAL(KIND=DP) :: SVM(NSTATV), FM(3,3), SM(6)
    ! ---------------------------------------------------------- JACOBIAN --- !
    EPS = SQRT(EPSILON(ONE))
    EPS = 1.E-10_DP
    DO I = 1, 6
       DF = DGEDDG(EPS/TWO, I, F)
       FP = F + DF
       SVP = STATEV
       CALL ANISOHYPEREL(NPROPS, PROPS, TEMP, FP, NOEL, CMNAME, INCMPFLAG, &
            IHYBFLAG, NFIBERS, FIBER_DIR, NSTATV, SVP, NFLDV, FIELDV, DFIELDV, SP)
       FM = F - DF
       SVM = STATEV
       CALL ANISOHYPEREL(NPROPS, PROPS, TEMP, FP, NOEL, CMNAME, INCMPFLAG, &
            IHYBFLAG, NFIBERS, FIBER_DIR, NSTATV, SVM, NFLDV, FIELDV, DFIELDV, SM)
       DDSDDE(:, I) = (SP - SM) / EPS
    END DO
    DDSDDE = HALF * (DDSDDE + TRANSPOSE(DDSDDE))
  END SUBROUTINE JACOBIAN

  ! ************************************************************************* !

  FUNCTION DGEDDG(EPS, I, F)
    ! ----------------------------------------------------------------------- !
    ! PERTURB THE DEFORMATION GRADIENT
    ! ----------------------------------------------------------------------- !
    USE TENSALG, ONLY: DYAD
    INTEGER, INTENT(IN) :: I
    REAL(KIND=DP), INTENT(IN) :: EPS, F(3,3)
    REAL(KIND=DP) :: DGEDDG(3,3)
    REAL(KIND=DP), PARAMETER :: O=1._DP,Z=0._DP
    REAL(KIND=DP), PARAMETER :: E(3,3)=RESHAPE((/O,Z,Z,Z,O,Z,Z,Z,O/),(/3,3/))
    INTEGER, PARAMETER :: IJ(2,6)=RESHAPE((/1,1,2,2,3,3,1,2,1,3,2,3/),(/2,6/))
    REAL(KIND=DP) :: EI(3), EJ(3), EIJ(3,3), EJI(3,3)
    ! ----------------------------------------------------------------------- !
    EI = E(IJ(1,I),:3)
    EJ = E(IJ(2,I),:3)
    EIJ = DYAD(EI, EJ)
    EJI = DYAD(EJ, EI)
    DGEDDG = EPS / TWO * (MATMUL(EIJ, F) + MATMUL(EJI, F))
  END FUNCTION DGEDDG

  ! ************************************************************************* !

END MODULE ANISOHYPERELASTIC

! *************************************************************************** !

SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,RPL,DDSDDT,DRPLDE,DRPLDT,&
     STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME, NDI,NSHR,NTENS,&
     NSTATV,PROPS,NPROPS,FIBER_DIR,NFIBERS,DROT,PNEWDT,CELENT,F0,F1,NOEL,NPT,&
     LAYER,KSPT,KSTEP,KINC)
  ! ------------------------------------------------------------------------- !
  !     UMAT INTERFACE FOR FINITE STRAIN PROPELLANT MODEL
  ! ------------------------------------------------------------------------- !
  USE ANISOHYPERELASTIC, ONLY: DP, ONE, TWO, THREE, ANISOHYPEREL, JACOBIAN
  IMPLICIT NONE

  ! --- PASSED ARGUMENTS
  ! ------------------------------------------------------------------------- !
  CHARACTER*8, INTENT(IN) :: CMNAME
  INTEGER, INTENT(IN) :: NDI, NSHR, NTENS, NSTATV, NPROPS, NFIBERS
  INTEGER, INTENT(IN) :: NOEL, NPT, LAYER, KSPT, KSTEP, KINC
  REAL(KIND=DP), INTENT(IN) :: SSE, SPD, SCD, RPL, DRPLDT, TIME(2), DTIME
  REAL(KIND=DP), INTENT(IN) :: TEMP, DTEMP, PNEWDT, CELENT
  REAL(KIND=DP), INTENT(INOUT) :: STRESS(NTENS), STATEV(NSTATV)
  REAL(KIND=DP), INTENT(INOUT) :: DDSDDE(NTENS, NTENS)
  REAL(KIND=DP), INTENT(INOUT) :: DDSDDT(NTENS), DRPLDE(NTENS)
  REAL(KIND=DP), INTENT(IN) :: STRAN(NTENS), DSTRAN(NTENS)
  REAL(KIND=DP), INTENT(IN) :: PREDEF(1), DPRED(1)
  REAL(KIND=DP), INTENT(IN) :: PROPS(NPROPS),FIBER_DIR(NFIBERS,3)
  REAL(KIND=DP), INTENT(IN) :: DROT(3, 3), F0(3, 3), F1(3, 3)
  ! --- LOCAL VARIABLES
  INTEGER, PARAMETER :: NFLDV=1, INCMPFLAG=1, IHYBFLAG=1
  REAL(KIND=DP) :: FIELDV(NFLDV), DFIELDV(NFLDV), ERR
  REAL(KIND=DP), ALLOCATABLE :: SNEO(:), CNEO(:,:)
  INTEGER :: N
  ! ---------------------------------------------------------------- UMAT --- !

  N = NPROPS - 1
  CALL ANISOHYPEREL(N, PROPS, TEMP, F1, NOEL, CMNAME, INCMPFLAG, IHYBFLAG, &
       NFIBERS, FIBER_DIR, NSTATV, STATEV, NFLDV, FIELDV, DFIELDV, STRESS)
  CALL JACOBIAN(N, PROPS, TEMP, F1, NOEL, CMNAME, INCMPFLAG, IHYBFLAG, &
       NFIBERS, FIBER_DIR, NSTATV, STATEV, NFLDV, FIELDV, DFIELDV, DDSDDE)

END SUBROUTINE UMAT

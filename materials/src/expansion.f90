MODULE EXPANSION
  ! ----------------------------------------------------------------------- !
  ! PROCEDURES TO DETERMINE STRESS FREE STRAIN DUE TO THERMAL EXPANSION
  !
  ! NOTE
  ! ----
  ! SYMMETRIC SECOND ORDER TENSOR ORDERING CORRESPONDS TO ABAQUS EXPLICIT,
  ! THAT IS, A SYMMETRIC SECOND ORDERING TENSOR A IS STORED AS
  !
  !                          | A(1)  A(4)  A(6) |
  !                          |       A(2)  A(5) |
  !                          |             A(3) |
  !
  ! THIS ORDERING DIFFERS FROM ABAQUS STANDARD THAT USES
  !
  !                          | A(1)  A(4)  A(5) |
  !                          |       A(2)  A(6) |
  !                          |             A(3) |
  !
  ! WHY CHOOSE THE FIRST? HABIT, AND IT IS THE ORDERING USED AT SANDIA, ANSYS,
  ! LSDYNA, ...
  !
  ! FOR USE IN MATMODLAB, THIS FILE MUST BE LINKED WITH mmlfio.f90
  ! ----------------------------------------------------------------------- !
  IMPLICIT NONE
  INTEGER, PARAMETER :: DP=SELECTED_REAL_KIND(14)
  INTEGER, PARAMETER :: NUI=1
  REAL(KIND=DP), PARAMETER :: ZERO=0._DP, ONE=1._DP, THREE=3._DP
  REAL(KIND=DP), PARAMETER :: P3RD=ONE/THREE
  REAL(KIND=DP), PARAMETER :: MACHINE_EPSILON=EPSILON(ONE)

CONTAINS

  SUBROUTINE MECHDEF(NPROP, PROPS, TEMPN, DTEMP, F, FM)
    ! ----------------------------------------------------------------------- !
    ! COMPUTE THE MECHANICAL DEFORMATION GRADIENT
    !
    ! NOTES
    ! -----
    ! DEFORMATION GRADIENT
    !
    !                    [F] = [Fm] [Fth]
    !
    ! WHERE, WITH ISOTROPIC THERMAL EXPANSION
    !
    !                   [Fth] = Jth^(1/3) [I]
    !
    ! SO
    !
    !                   [Fm] = Jth^(-1/3) [F]
    ! NOTE
    ! ----
    ! THIS PROCEDURE CALLS THERMAL_EXPANSION TO GET THE LINEAR THERMAL STRAIN
    ! FROM THE CTE AND COMPUTES THE THERMAL DEFORMATION. THE THERMAL EXPANSION
    ! PROCEDURE COULD BE GENERALIZED TO INTERPOLATE FROM A TABLE RATHER THAN
    ! USE A CONSTANT CTE. THEN, AN INITIAL THERMAL STRAIN COULD BE USED.
    ! ----------------------------------------------------------------------- !
    INTEGER :: NPROP
    REAL(KIND=DP), INTENT(IN) :: PROPS(NPROP), TEMPN, DTEMP, F(3,3)
    REAL(KIND=DP), INTENT(OUT) :: FM(3,3)
    REAL(KIND=DP) :: CTE, FAC1, FAC2, FAC, TEMP, THS0, THS
    CHARACTER*120 :: JNKSTR
    INTEGER :: INTV(1)
    REAL(KIND=DP) :: REALV(1)
    CHARACTER*8 :: CHARV(1)
    ! ----------------------------------------------------------------------- !

    FM = F

    IF (NPROP == 1) THEN
       ! ISOTROPIC EXPANSION
       CTE = PROPS(1)
       IF (ABS(CTE) < MACHINE_EPSILON) RETURN

       ! EVALUATE THE THERMAL STRAIN. IF THERMAL_EXPANSION IS GENERALIZED,
       ! COULD BE USED TO GET THS0
       THS0 = ZERO
       TEMP = TEMPN + DTEMP
       CALL THERMAL_EXPANSION(CTE, TEMP, DTEMP, THS)
       FAC1 = EXP(THS0) ** 3
       FAC2 = EXP(THS) ** 3

       ! COMPUTE THE MECHANICAL DEFORMATION GRADIENT
       FAC = (FAC1 / FAC2) ** P3RD
       FM = FAC * F

    ELSE
       JNKSTR = "NON-ISOTROPIC EXPANSION NOT YET SUPPORTED"
       CALL EXPANSION_ERR(-3, JNKSTR, INTV, REALV, CHARV)
    END IF

    RETURN

  END SUBROUTINE MECHDEF

  ! ************************************************************************* !

  SUBROUTINE THERMAL_EXPANSION(CTE, TEMP, DTEMP, THS)
    ! COMPUTE (LINEAR) THERMAL EXPANSION
    REAL(KIND=DP), INTENT(IN) :: CTE, TEMP, DTEMP
    REAL(KIND=DP), INTENT(OUT) :: THS
    THS = CTE * DTEMP
  END SUBROUTINE THERMAL_EXPANSION

END MODULE EXPANSION

SUBROUTINE EXPANSION_ERR(I, MSG, INTV, REALV, CHARV)
  ! BORROWS FROM ABAQUS' STDB_ABQERR. FOR USE IN ABAQUS, JUST REPLACE CODE
  ! BELOW WITH CALL TO STDB_ABQERR WITH SAME ARGUMENTS AS THIS PROCEDURE
  IMPLICIT NONE
  INTEGER :: I
  CHARACTER*120 :: MSG
  INTEGER :: INTV(1), IDUM
  REAL(8) :: REALV(1), RDUM
  CHARACTER*8 :: CHARV(1), CDUM(1)
  CHARACTER*200 JNKSTR
  EXTERNAL LOG_MESSAGE
  EXTERNAL LOG_ERROR
  EXTERNAL LOG_WARNING
  RDUM=REALV(1)
  IDUM=INTV(1)
  CDUM=CHARV(1)
  IF (I == -3) THEN
     JNKSTR = "EXPANSION: BOMBED: " // MSG
     CALL LOG_ERROR(JNKSTR)
  ELSE IF (I == -1) THEN
     CALL LOG_WARNING(MSG)
  ELSE
     CALL LOG_MESSAGE(MSG)
  END IF
  RETURN
END SUBROUTINE EXPANSION_ERR
